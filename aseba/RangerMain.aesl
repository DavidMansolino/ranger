<!DOCTYPE aesl-source>
<network>


<!--list of global events-->
<event size="13" name="mainFeedback"/>
<event size="17" name="mainFeedbackWithEncoders"/>
<event size="3" name="setLed"/>
<event size="10" name="receiverFeedback"/>
<event size="2" name="setSpeed"/>
<event size="1" name="enableEncoders"/>
<event size="2" name="playLedVid"/>
<event size="0" name="stopLedVid"/>
<event size="11" name="neuilFeedback"/>
<event size="8" name="neuilEvent"/>
<event size="10" name="customEvent"/>
<event size="7" name="emitterEvent"/>
<event size="1" name="enableFeedback"/>


<!--list of constants-->
<constant value="15" name="BALANCE_FILTER_SIZE"/>
<constant value="600" name="CURRENT_LIMIT"/>
<constant value="13" name="NB_TIMER0_CYCLES"/>
<constant value="15" name="SPEED_EYELID"/>
<constant value="10" name="SPEED_EYE"/>
<constant value="35" name="SPEED_EYELID_FAST"/>
<constant value="60" name="POS_REACHED_STEPS"/>
<constant value="270" name="EYES_RAYON_L"/>
<constant value="270" name="EYES_RAYON_R"/>


<!--show keywords state-->
<keywords flag="true"/>


<!--node RangerMain-->
<node nodeId="1" name="RangerMain">var touchRightThreshold[9] = [1492, 840, 1156, 742, 825, 0, 1372, 869, 1122]
var touchLeftThreshold[9] = [0, 440, 594, 368, 0, 359, 0, 421, 535]
#var touchRearThreshold[9] = [671, 0, 0, 0, 386, 462, 1039, 594, 569]
#var touchRearThreshold[9] = [675, 0, 0, 377, 769, 469, 1061, 599, 524]
var touchRearThreshold[9] = [676, 460, 0, 0, 389, 465, 1055, 610, 576]
var touchBuffer[2]
var i
var j
var timer = 0
var encoderEnabled = 0
var playingVid = 0
var repeatVid = 0
var TouchRight
var TouchLeft
var TouchRear
var TmpBumber = 0
var mainFeedbackEnable = 0

mot1.pid.current_max = 3500
mot2.pid.current_max = 3500

# switch off all leds
for i in 0:185 do
	call led.set(i, 0, 0, 0)
end

# touch panel calibration
for i in 0:8 do
	if i > 4 then # because 5 and 6 are not for touch panel
		j = 163 + i + 2 
	else
		j = 163 + i
	end
	# right
	touchBuffer[0] = j
	touchBuffer[1] = touchRightThreshold[i]&lt;&lt;2 | 1
	call touch.debug(0, touchBuffer, 0)
	# rear
	touchBuffer[0] = j
	touchBuffer[1] = touchRearThreshold[i]&lt;&lt;2 | 1
	call touch.debug(1, touchBuffer, 0)
	# left
	touchBuffer[0] = j
	touchBuffer[1] = touchLeftThreshold[i]&lt;&lt;2 | 1
	call touch.debug(2, touchBuffer, 0)
end
touch._disable = 0

# stop motors
mot1.pid.enable = 1
mot1.pid.target_current = 0
mot1.pid.target_speed = 0
mot2.pid.enable = 1
mot2.pid.target_current = 0
mot2.pid.target_speed = 0

onevent setLed
for i in 0:185 do
  call led.set(i, event.args[0], event.args[1], event.args[2])
end

# Simulate bumber with the motor current
sub computeTmpBumber
if mot1.pid.target_current > CURRENT_LIMIT or mot1.pid.target_current &lt; -CURRENT_LIMIT or mot2.pid.target_current > CURRENT_LIMIT or mot2.pid.target_current &lt; -CURRENT_LIMIT then
	TmpBumber = 1
else
	TmpBumber = 0
end

# Compress touchSensors in order to use only one variable by pannel
sub touchCompress
TouchRight = 0
TouchLeft = 0
TouchRear = 0
j = 1
for i in 0:8 do
	TouchRight += touch.right[i]*j
	TouchLeft  += touch.left[i]*j
	TouchRear  += touch.rear[i]*j
	j *= 2
end


#event acc used to send data
onevent acc
if mainFeedbackEnable != 0 then
	callsub touchCompress
	callsub computeTmpBumber
	if encoderEnabled == 1 then
		emit mainFeedbackWithEncoders[acc, sharp, vbat, TmpBumber, mot1.speed, mot2.speed, TouchRight, TouchLeft, TouchRear, mot1.enc._pulse, mot2.enc._pulse, _status.ExtPower]
	else
		emit mainFeedback[acc, sharp, vbat, TmpBumber, mot1.speed, mot2.speed, TouchRight, TouchLeft, TouchRear, _status.ExtPower]
	end
end
if _status.ExtPower == 1 then
	call led.vid.stop()
end



onevent setSpeed
mot1.pid.enable = 2
mot1.pid.target_speed = event.args[0]
mot2.pid.enable = 2
mot2.pid.target_speed = event.args[1]

onevent enableEncoders
encoderEnabled = event.args[0]

onevent playLedVid
repeatVid = 0
call led.vid.stop()
playingVid = event.args[0]
repeatVid = event.args[1]
call led.vid.start(playingVid)

onevent led
if repeatVid == 1 then
	call led.vid.start(playingVid)
else
	call led.vid.stop()
end

onevent stopLedVid
call led.vid.stop()
repeatVid = 0

onevent enableFeedback
mainFeedbackEnable = event.args[0]
</node>


<!--node rab2-->
<node nodeId="7" name="rab2">var rabFeedbackEnable = 0
var c
rf.id = 7
rf.swarm_size = 25
rf.slot_time = 4000
rx.power.en = 1
tx.power.en = 1
#call rf.start()


onevent packet.rx
if rabFeedbackEnable != 0 then
	emit receiverFeedback[rx.source, rx.angle, rx.dist, rx.data]
end


onevent emitterEvent
for c in 0:6 do
	rf.tx_data[c] = event.args[c]
end

onevent enableFeedback
rabFeedbackEnable = event.args[0]</node>


<!--node neuil-->
<node nodeId="255" name="neuil">var mainFeedbackEnable = 0
var i
var j
var open_oeil_in_X_timer0_cycles
var oeil_ouvert[2]
var pos_min[8] = [582,695,790,887,810,786,629,764]
var pos_center[8] = [872,975,1000,1000,1000,1000,909,1014]
var pos_max[8] = [1172,1285,1198,1292,1220,1190,1199,1314]
var pos_inc[8] = [SPEED_EYE,SPEED_EYE,
				  SPEED_EYELID,SPEED_EYELID,
				  SPEED_EYELID,SPEED_EYELID,
				  SPEED_EYE,SPEED_EYE]
var pos_target[8]
var pos_actual[8]
var selectedServo = 0
var pwm_selected = 0
var min[8]
var max[8]
var posReached[8]
var color = 0

var speed

var balanceValues[BALANCE_FILTER_SIZE]
var sortedBalanceValues[BALANCE_FILTER_SIZE]
var filteredBalance
call math.fill(balanceValues, balance)

pos_target = pos_center
call math.fill(pos_actual, 0)

timer[0] = 10
timer[1] = 100
sharp.enable[0] = 1
sharp.enable[1] = 1
sharp.enable[2] = 1

pos_target[2]=pos_min[2]
pos_target[3]=pos_max[3]
pos_target[4]=pos_min[4]
pos_target[5]=pos_max[5]

#sub filterBalance
#for i in 2:BALANCE_FILTER_SIZE do
#	balanceValues[i-2] = balanceValues[i-1]
#end
#balanceValues[BALANCE_FILTER_SIZE-1] = balance
#sortedBalanceValues = balanceValues
#call math.sort(sortedBalanceValues)
#filteredBalance = sortedBalanceValues[(BALANCE_FILTER_SIZE+1)/2]

sub call_Ouvert
pos_target[2]=pos_min[2]
pos_target[3]=pos_max[3]
pos_target[4]=pos_min[4]
pos_target[5]=pos_max[5]

sub call_Ferme
pos_target[2]=pos_max[2]
pos_target[3]=pos_min[3]
pos_target[4]=pos_max[4]
pos_target[5]=pos_min[5]

sub call_Haut
pos_target[0]=1122
pos_target[1]=975
pos_target[6]=909
pos_target[7]=784

sub call_Bas
pos_target[0]=642
pos_target[1]=975
pos_target[6]=909
pos_target[7]=1264

sub call_Gauche
pos_target[0]=872
pos_target[1]=730
pos_target[6]=686
pos_target[7]=1014

sub call_Droite
pos_target[0]=872
pos_target[1]=1245
pos_target[6]=1165
pos_target[7]=1014

onevent customEvent
if event.args[0] == 1 then #clin d'oeil
	call math.fill(pos_inc[2:5] , SPEED_EYELID_FAST)
	callsub call_Ferme
	open_oeil_in_X_timer0_cycles = 2 * NB_TIMER0_CYCLES
	if event.args[1] == 1 then
		callsub call_Gauche
	elseif event.args[1] == 2 then
		callsub call_Droite
	end
end

onevent timer0
if open_oeil_in_X_timer0_cycles > 0 then
	open_oeil_in_X_timer0_cycles--
	if  open_oeil_in_X_timer0_cycles == NB_TIMER0_CYCLES then
		callsub call_Ouvert
##		servo.pwm[2:5] = pos_target[2:5]
##		pos_actual[2:5] = pos_target[2:5]
	end
	if  open_oeil_in_X_timer0_cycles == 0 then
		call math.fill(pos_inc[2:5] , SPEED_EYELID)
##		servo.pwm[2:5] = pos_target[2:5]
##		pos_actual[2:5] = pos_target[2:5]
	end
end

for i in 0:7 do
	if pos_actual[i] == 0 then
		pos_actual[i] = pos_target[i]
		servo.pwm[i] = pos_target[i]
	elseif pos_target[i] == pos_actual[i] then	
		if posReached[i] != 1 then	
			if posReached[i] == 0 then
				posReached[i] = 1
				servo.pwm[i] = 0
			else
				posReached[i]++
			end
		end
	else
		if pos_target[i] > pos_actual[i] then
			call math.min(pos_actual[i], pos_target[i], pos_actual[i]+pos_inc[i])
		else
			call math.max(pos_actual[i], pos_target[i], pos_actual[i]-pos_inc[i])
		end
		servo.pwm[i] = pos_actual[i]
		posReached[i] = -POS_REACHED_STEPS
	end
end

when lolette == 1 do
	if color == 0 then
		color = 1
		emit setLed[255, 0, 0]
	elseif color == 1 then
		color = 2
		emit setLed[0, 255, 0]
	elseif color == 2 then
		color = 3
		emit setLed[0, 0, 255]
	elseif color == 3 then
		color = 0
		emit setLed[255, 255, 255]
	end

end

when lolette == 0 do
	emit setLed[0, 0, 0]
end

when sharp.value[0] > 1500 do
	callsub call_Droite
end

when sharp.value[2] > 1500 do
	callsub call_Gauche
end

when sharp.value[1] > 1500 do
	call math.fill(pos_inc[2:5] , SPEED_EYELID_FAST)
	callsub call_Ferme
	open_oeil_in_X_timer0_cycles = 2 * NB_TIMER0_CYCLES
end

# feedback 
onevent timer1
#callsub filterBalance
if mainFeedbackEnable != 0 then
	for i in 0:3 do # Send position of the eyelid relative to the close position
		#RelativePosition[i] = position[i+2] - EyeLidClose[i]
	end
	emit neuilFeedback [sharp.value, 0, 0, 0, 0, 0, 0, lolette, filteredBalance]
end

### Command received ###
onevent neuilEvent
## EyeLids
#pos_target[2] = ((100-event.args[4]) * ((pos_max[2] - pos_min[2])/10)) / 10 + pos_min[2]

#pos_target[3] = (event.args[5] * ((pos_max[3] - pos_min[3])/10)) / 10 + pos_min[3]

#pos_target[4] = ((100-event.args[6]) * ((pos_max[4] - pos_min[4])/10)) / 10 + pos_min[4]

#pos_target[5] = (event.args[7] * ((pos_max[5] - pos_min[5])/10)) / 10 + pos_min[5]

##Eyes
#Make sure consign is between -100 and 100
for i in 0:3 do
	if event.args[i] > 100 then
		event.args[i] = 100
	elseif event.args[i] &lt; -100 then
		event.args[i] = -100
	end
end

# Left eye
j = event.args[0] * event.args[0] + event.args[1] * event.args[1]
call math.sqrt(j, j)
if j > 100 then # out of circle
	event.args[0] = (event.args[0] * 100) / j
	event.args[1] = (event.args[1] * 100) / j
end

# Right eye
j = event.args[2] * event.args[2] + event.args[3] * event.args[3]
call math.sqrt(j, j)
if j > 100 then # out of circle
	event.args[2] = (event.args[2] * 100) / j
	event.args[3] = (event.args[3] * 100) / j
end


if event.args[0] &lt; 0 and event.args[1] > 0 then
	event.args[0] = (event.args[0] * 7) / 10
	event.args[1] = (event.args[1] * 7) / 10
end
if event.args[0] &lt; 0 and event.args[1] > 0 then
	event.args[0] = (event.args[0] * 7) / 10
	event.args[1] = (event.args[1] * 7) / 10
end
if event.args[2] &lt; 0 and event.args[3] &lt; 0 then
	event.args[2] = (event.args[2] * 7) / 10
	event.args[3] = (event.args[3] * 7) / 10
end

# apply
pos_target[0] = pos_center[0] + (event.args[0] * EYES_RAYON_L) / 100
pos_target[1] = pos_center[1] + (event.args[1] * EYES_RAYON_L) / 100
pos_target[6] = pos_center[6] + (event.args[3] * EYES_RAYON_R) / 100
pos_target[7] = pos_center[7] + (-event.args[2] * EYES_RAYON_R) / 100


onevent enableFeedback
mainFeedbackEnable = event.args[0]
</node>


</network>
